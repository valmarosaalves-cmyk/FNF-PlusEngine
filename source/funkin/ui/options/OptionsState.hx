package funkin.ui.options;

import funkin.ui.mainmenu.MainMenuState;
import funkin.data.stage.StageData;

class OptionsState extends MusicBeatState
{
	var options:Array<String> = [];
	
	function buildOptions():Array<String> {
		var opts:Array<String> = [];
		if (ClientPrefs.data.colorQuantization == false) opts.push('Note Colors');
		opts.push('Controls');
		opts.push('Adjust Delay and Combo');
		opts.push('Graphics');
		opts.push('Visuals');
		opts.push('Gameplay');
		opts.push('Legacy');
		#if MODCHARTS_NOTITG_ALLOWED opts.push('Modchart'); #end
		#if TRANSLATIONS_ALLOWED opts.push('Language'); #end
		#if mobile opts.push('Mobile'); #end
		return opts;
	}
	
	private var grpOptions:FlxTypedGroup<Alphabet>;
	private static var curSelected:Int = 0;
	var lerpSelected:Float = 0;
	public static var menuBG:FlxSprite;
	public static var onPlayState:Bool = false;
	
	#if mobile
	var touchScroll:funkin.mobile.backend.TouchScroll;
	#end

	function openSelectedSubstate(label:String) {
		if (label != "Adjust Delay and Combo"){
			removeTouchPad();
			persistentUpdate = false;
			#if mobile
			if (touchScroll != null) touchScroll.reset(); // Reset tap state when opening substate
			#end
		}
		switch(label)
		{
			case 'Note Colors':
			if (ClientPrefs.data.colorQuantization == false) {
				openSubState(new funkin.ui.options.NotesColorSubState());
			}
			case 'Controls':
				openSubState(new funkin.ui.options.ControlsSubState());
			case 'Graphics':
				openSubState(new funkin.ui.options.GraphicsSettingsSubState());
			case 'Visuals':
				openSubState(new funkin.ui.options.VisualsSettingsSubState());
			case 'Gameplay':
				openSubState(new funkin.ui.options.GameplaySettingsSubState());
			case 'Legacy':
				openSubState(new funkin.ui.options.LegacySettingsSubState());
			case 'Modchart':
				openSubState(new funkin.ui.options.ModchartSettingsSubState());
			case 'Adjust Delay and Combo':
				MusicBeatState.switchState(new funkin.ui.options.NoteOffsetState());
			case 'Mobile':
				openSubState(new funkin.mobile.options.MobileSettingsSubState());
			case 'Language':
				openSubState(new funkin.ui.options.LanguageSubState());
		}
	}

	var selectorLeft:Alphabet;
	var selectorRight:Alphabet;

	override function create()
	{
		#if DISCORD_ALLOWED
		DiscordClient.changePresence("Options Menu", null);
		#end
		
		options = buildOptions();

		var bg:FlxSprite = new FlxSprite().loadGraphic(Paths.image('menuDesat'));
		bg.antialiasing = ClientPrefs.data.antialiasing;
		bg.color = 0xFFea71fd;
		bg.updateHitbox();

		bg.screenCenter();
		add(bg);

		if (controls.mobileC)
		{
			var tipText:FlxText = new FlxText(150, FlxG.height - 24, 0, Language.getPhrase('mobile_controls_tip', 'Press {1} to Go Mobile Controls Menu', [(FlxG.onMobile ? 'C' : 'CTRL or C')]), 16);
			tipText.setFormat("PhantomMuff 1.5", 17, FlxColor.WHITE, LEFT, FlxTextBorderStyle.OUTLINE, FlxColor.BLACK);
			tipText.borderSize = 1.25;
			tipText.scrollFactor.set();
			tipText.antialiasing = ClientPrefs.data.antialiasing;
			add(tipText);
		}

		grpOptions = new FlxTypedGroup<Alphabet>();
		add(grpOptions);

		for (num => option in options)
		{
			var optionText:Alphabet = new Alphabet(0, 0, Language.getPhrase('options_$option', option), true);
			optionText.targetY = num;
			optionText.isMenuItem = true;
			grpOptions.add(optionText);
		}

		selectorLeft = new Alphabet(0, 0, '>', true);
		add(selectorLeft);
		selectorRight = new Alphabet(0, 0, '<', true);
		add(selectorRight);

		lerpSelected = curSelected;
		changeSelection();
		ClientPrefs.saveSettings();
		
		#if mobile
		// Initialize touch scroll
		touchScroll = new funkin.mobile.backend.TouchScroll(true);
		funkin.mobile.backend.TouchUtil.setScrollHandler(touchScroll);
		#end
		
		// Posicionar elementos sin animación inicial
		for (num => item in grpOptions.members)
		{
			var targetY:Float = item.targetY - lerpSelected;
			item.screenCenter(X);
			#if mobile
			// Adjust Y position higher on mobile when near the end of the list
			var yOffset:Float = (curSelected >= options.length - 2) ? 0.1 : 0.2;
			item.y = (FlxG.height * yOffset) + (targetY * 50);
			#else
			item.y = (FlxG.height * 0.2) + (targetY * 50);
			#end
			
			item.alpha = 0.6;
			if (item.targetY == curSelected)
			{
				item.alpha = 1;
				selectorLeft.x = item.x - 63;
				selectorLeft.y = item.y;
				selectorRight.x = item.x + item.width + 15;
				selectorRight.y = item.y;
			}
		}

		addTouchPad('UP_DOWN', 'A_B_C');

		super.create();
	}

	override function closeSubState()
	{
		super.closeSubState();
		ClientPrefs.saveSettings();
		#if DISCORD_ALLOWED
		DiscordClient.changePresence("Options Menu", null);
		#end
		controls.isInSubstate = false;
		removeTouchPad();
		addTouchPad('NONE', 'B_C');
		persistentUpdate = true;
		
		#if mobile
		// Reset touch state and refresh selection when returning from substate
		if (touchScroll != null)
		{
			touchScroll.reset();
		}
		// Force refresh UI
		changeSelection(0);
		#end
	}

	var exiting = false;
	override function update(elapsed:Float) {
		super.update(elapsed);

		lerpSelected = FlxMath.lerp(curSelected, lerpSelected, Math.exp(-elapsed * 9.6));

		for (num => item in grpOptions.members)
		{
			var targetY:Float = item.targetY - lerpSelected;
			item.screenCenter(X);
			#if mobile
			// Adjust Y position higher on mobile when near the end of the list
			var yOffset:Float = (curSelected >= options.length - 4) ? 0.01 : 0.2;
			item.y = FlxMath.lerp((FlxG.height * yOffset) + (targetY * 50), item.y, Math.exp(-elapsed * 10.2));
			#else
			item.y = FlxMath.lerp((FlxG.height * 0.2) + (targetY * 50), item.y, Math.exp(-elapsed * 10.2));
			#end
			
			item.alpha = 0.6;
			if (item.targetY == curSelected)
			{
				item.alpha = 1;
				selectorLeft.x = item.x - 63;
				selectorLeft.y = item.y;
				selectorRight.x = item.x + item.width + 15;
				selectorRight.y = item.y;
			}
		}

		if(!exiting) {
			if (controls.UI_UP_P)
				changeSelection(-1);
			if (controls.UI_DOWN_P)
				changeSelection(1);
			
			#if mobile
			// Touch scroll handling with smooth scrolling
			if (touchScroll != null)
			{
				var scrollDelta = touchScroll.update();
				
				// Apply continuous scroll
				if (Math.abs(scrollDelta) > 0.5)
				{
					// Smooth continuous scrolling (inverted for natural direction)
					lerpSelected += -scrollDelta / 150;
					lerpSelected = FlxMath.bound(lerpSelected, 0, options.length - 1);
					
					// Update curSelected when crossing integer boundaries
					var newSelected = Math.round(lerpSelected);
					if (newSelected != curSelected)
					{
						changeSelection(newSelected - curSelected);
						// Keep lerp smooth, don't force snap
					}
				}
				
				// Handle tap on options (only if not scrolling)
				if (touchScroll.wasTapped())
				{
					handleTouchOptions();
				}
			}
			#end
			
			if ((touchPad != null && touchPad.buttonC != null && touchPad.buttonC.justPressed) || (FlxG.keys.justPressed.CONTROL && controls.mobileC))
			{
				persistentUpdate = false;
				openSubState(new funkin.mobile.substates.MobileControlSelectSubState());
			}

			if (controls.BACK)
			{
				exiting = true;
				FlxG.sound.play(Paths.sound('cancelMenu'));
				if(onPlayState)
				{
					StageData.loadDirectory(PlayState.SONG);
					LoadingState.loadAndSwitchState(new PlayState());
					FlxG.sound.music.volume = 0;
				}
				else 
				{
					MusicBeatState.switchState(new MainMenuState());
				}
			}
			else if (controls.ACCEPT) openSelectedSubstate(options[curSelected]);
		}
	}
	
	#if mobile
	function handleTouchOptions():Void
	{
		var tapPos = touchScroll.getTapPosition();
		if (tapPos == null) return;
		
		for (i in 0...grpOptions.members.length)
		{
			var item = grpOptions.members[i];
			if (item != null && item.visible && item.overlapsPoint(new FlxPoint(tapPos.x, tapPos.y)))
			{
				if (i == curSelected)
				{
					// Tapped on selected item - open it
					openSelectedSubstate(options[curSelected]);
				}
				else
				{
					// Tapped on different item - select it
					var prevSelected = curSelected;
					curSelected = i;
					lerpSelected = i;
					for (num => optionItem in grpOptions.members)
					{
						optionItem.targetY = num;
					}
					FlxG.sound.play(Paths.sound('scrollMenu'));
				}
				break;
			}
		}
	}
	#end
	
	function changeSelection(change:Int = 0)
	{
		curSelected = FlxMath.wrap(curSelected + change, 0, options.length - 1);

		for (num => item in grpOptions.members)
		{
			item.targetY = num;
		}
		
		if(change != 0) FlxG.sound.play(Paths.sound('scrollMenu'));
	}

	override function destroy()
	{
		#if mobile
		if (touchScroll != null)
		{
			touchScroll.destroy();
			touchScroll = null;
		}
		funkin.mobile.backend.TouchUtil.clearScrollHandler();
		#end
		
		ClientPrefs.loadPrefs();
		super.destroy();
	}
}